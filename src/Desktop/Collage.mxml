<?xml version="1.0" encoding="utf-8"?>
<mx:WindowedApplication
	xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns:Collage="Collage.*"
	xmlns:clg="Collage.Document.*"
	xmlns:degrafa="http://www.degrafa.com/2007"
	layout="absolute"
	minHeight="400"
	minWidth="500"
	height="700"
	width="1000"
	menu="{nativeMenu}"
	title="Collage"
	buttonAlignment="auto"
	cornerRadius="4"
	showStatusBar="true"
	borderThickness="1"
	borderColor="#999999"
	dropShadowEnabled="true"
	applicationComplete="Initialize()"
    paste="HandlePaste(event);"
    copy="HandleCopy(event);"
    cut="HandleCut(event);"
	keyDown="HandleKeyDown(event);"
	nativeDragEnter="onDragIn(event)"
	nativeDragDrop="onDrop(event)"
	invoke="OnInvoke(event)"
	horizontalScrollPolicy="off"
	verticalScrollPolicy="off"
	closing="WindowClosing(event)" >
	<mx:Style source="../Common/yflexskin.css"/>
	<mx:Style source="../Common/collageBasic.css"/>
	<mx:Script>
	<![CDATA[
		import com.adobe.serialization.json.JSON;
		import mx.containers.dividedBoxClasses.BoxDivider;
		import mx.graphics.ImageSnapshot;
		import mx.graphics.codec.*;
		import mx.controls.Alert;
		import mx.managers.PopUpManager;
		import mx.core.IFlexDisplayObject;
		import mx.events.*;
		import flash.events.*;
		import flash.filesystem.*;
		import flash.desktop.Clipboard;
	    import flash.desktop.ClipboardFormats;
	    import flash.desktop.ClipboardTransferMode;
		import Collage.Document.*;
		import Collage.Clip.*;
		import Collage.Clips.Picture.*;
		import Collage.Clips.Label.*;
		import Collage.Clips.TextBox.*;
		import Collage.DataEngine.*;
		import Collage.DataEngine.Storage.*;
		import Collage.Logger.*;
		import org.alivepdf.pdf.*;
		import org.alivepdf.pdf.PDF;
		import org.alivepdf.display.*;
		import org.alivepdf.images.*;
		import org.alivepdf.layout.*;
		import org.alivepdf.saving.*;

		/* auto updater ui */
		import air.update.events.UpdateEvent;
		import air.update.ApplicationUpdaterUI;

		private var appUpdater:ApplicationUpdaterUI = new ApplicationUpdaterUI();

		private function checkUpdate():void {
			appUpdater.updateURL = "http://version.endlesspaths.com/collage/latest.xml";
			appUpdater.addEventListener(UpdateEvent.INITIALIZED, onUpdate);
			/*appUpdater.addEventListener(ErrorEvent.ERROR, onError);*/
			appUpdater.isCheckForUpdateVisible = false;
			appUpdater.initialize();
		}

		private function OnInvoke(event:InvokeEvent):void {
			for (var i:uint = 0; i < event.arguments.length; i++)
			{
				if (!event.currentDirectory || !event.currentDirectory.isDirectory)
					return;
				Logger.Log("Application Arugment: " + event.arguments[i], LogEntry.DEBUG, this);
				var newFile:File = event.currentDirectory.resolvePath(event.arguments[i]);
				if (newFile)
					OpenFileObject(newFile);
			}
		}

		private function onUpdate(event:UpdateEvent):void {
			//start the process of checking for a new update and to install
			appUpdater.checkNow();
		}

		private var _Document:Document;
		private var _Dashboard:DashboardFile; // cloud dashboard file
		private var _DashboardImages:Array;

		private function Initialize():void
		{
			_DashboardImages = new Array();
			
			Logger.events.addEventListener(Logger.NEW_LOG_EVENT, LoggerStatusBarUpdate);
			Logger.Log("Application Started", LogEntry.DEBUG   , this);
			//stage.addEventListener(KeyboardEvent.KEY_DOWN, HandleKeyDown);

			Session.events.addEventListener(Session.LOGIN_SUCCESS, HandleLoginSuccess);
			Session.events.addEventListener(Session.TOKEN_EXPIRED, HandleTokenExpired);
			Session.CheckToken();

			_EditDocumentView.InitializeForEdit(inspectorWindow, optionsBox);

			this.nativeWindow.x = (Capabilities.screenResolutionX - this.width)/2;
			this.nativeWindow.y = (Capabilities.screenResolutionY - this.height)/2;
			this.nativeWindow.visible = true;

			/* auto updater - called after everything else is initialized */
			checkUpdate();
		}

		public function LoggerStatusBarUpdate(event:Event):void {
			if (Logger.LastLog() != null && Logger.LastLog().level >= 0)//LogEntry.INFO)
				status = Logger.LastLog().text;
		}

		public function HandleLoginSuccess(event:Event):void {
			welcomeScreen.visible = false;
			DataEngine.LoadAllDataSets();
		}

		public function HandleLoginFailure(event:Event):void {
			welcomeScreen.visible = true;
		}

		public function HandleTokenExpired(event:Event):void {
			welcomeScreen.visible = true;
		}

		private function StandardOSModifier(item:Object):Array{
			var modifiers:Array = new Array();
			if((Capabilities.os.indexOf("Windows") >= 0)){
				modifiers.push(Keyboard.CONTROL);
			} else if (Capabilities.os.indexOf("Mac OS") >= 0){
				modifiers.push(Keyboard.COMMAND);
			}
			return modifiers;
		}

		private function Fullscreen():void
		{
			if (stage.displayState == StageDisplayState.FULL_SCREEN_INTERACTIVE)
				stage.displayState = StageDisplayState.NORMAL;
			else
				stage.displayState = StageDisplayState.FULL_SCREEN_INTERACTIVE;
		}

		private function HandleKeyDown(event:KeyboardEvent):void
		{
//			if (event.keyCode == KeyCodes.DELETE || event.keyCode == KeyCodes.BACKSPACE)
//				_EditDocumentView.deleteSelected();
		}

		private function WindowClosing(event:Event):void
		{

		}

		public function HandleCopy(event:Event):void
		{
			if (!_EditDocumentView.IsObjectSelected())
				return;
			var copyObject:Object = _EditDocumentView.currentlySelected.SaveToObject();
			Clipboard.generalClipboard.clear();
			Clipboard.generalClipboard.setData("epaths:clipObject", JSON.encode(copyObject));
			
			if (_EditDocumentView.currentlySelected is LabelClip)
				Clipboard.generalClipboard.setData(ClipboardFormats.TEXT_FORMAT, (_EditDocumentView.currentlySelected as LabelClip).text);
			else if (_EditDocumentView.currentlySelected is TextBoxClip)
				Clipboard.generalClipboard.setData(ClipboardFormats.HTML_FORMAT, (_EditDocumentView.currentlySelected as TextBoxClip).text);
			_EditDocumentView.currentlySelected.selected = false;
			Clipboard.generalClipboard.setData(ClipboardFormats.BITMAP_FORMAT, ImageSnapshot.captureBitmapData(_EditDocumentView.currentlySelected.view));
			_EditDocumentView.currentlySelected.selected = true;
		}
		
		public function HandleCut(event:Event):void
		{
			if (!_EditDocumentView.IsObjectSelected())
				return;
			var selectedClip:Clip = _EditDocumentView.currentlySelected;
			HandleCopy(null);
			_EditDocumentView.deleteClip(selectedClip);
		}
		
		public function HandlePaste(event:Event):void
		{
			if (Clipboard.generalClipboard.hasFormat("epaths:clipObject")) {
				var clipDataObject:Object = JSON.decode(Clipboard.generalClipboard.getData("epaths:clipObject") as String);
				if (!clipDataObject || !clipDataObject["type"])
					return;
				var newClip:Clip = _EditDocumentView.AddClipByType(clipDataObject["type"]);
				if (newClip) {
					newClip.LoadFromObject(clipDataObject);
					newClip.x = 17;
					newClip.y = 17;
				}
			} else if (Clipboard.generalClipboard.hasFormat(ClipboardFormats.BITMAP_FORMAT)) {
				/*var clip:PictureClip = _EditDocumentView.AddClipFromData(Clipboard.generalClipboard.getData(ClipboardFormats.BITMAP_FORMAT) as BitmapData) as PictureClip;*/
				var clip:PictureClip = _EditDocumentView.AddClipByType('image', new Rectangle(150, 150, 300, 300)) as PictureClip;
				clip.addEventListener(PictureClip.IMAGE_LOADED, PictureClip_ImageLoaded);
				
				clip.LoadFromData( Clipboard.generalClipboard.getData(ClipboardFormats.BITMAP_FORMAT) as BitmapData );
				
				Logger.Log("Bitmap Pasted", LogEntry.INFO, this);
			} 
/*			else if (Clipboard.generalClipboard.hasFormat(ClipboardFormats.HTML_FORMAT)) {
				_EditDocumentView.AddClipFromData(Clipboard.generalClipboard.getData(ClipboardFormats.HTML_FORMAT) as String);
				Logger.Log("HTML Pasted", LogEntry.INFO, this);
			} else if (Clipboard.generalClipboard.hasFormat(ClipboardFormats.TEXT_FORMAT)) {
				_EditDocumentView.AddClipFromData(Clipboard.generalClipboard.getData(ClipboardFormats.TEXT_FORMAT) as String);
				Logger.Log("Text Pasted", LogEntry.INFO, this);
			}
*/		}

		public function SaveFile():void
		{
			var file:File = File.desktopDirectory.resolvePath("file.clg");
			file.addEventListener(Event.SELECT, SaveFileEvent);
			_EditDocumentView.ClearSelection();
			file.browseForSave("Save As");
		}

		protected function SaveFileEvent(event:Event):void
		{
			var jsonFile:String = JSON.encode(_EditDocumentView.model.SaveToObject());

			var newFile:File = event.target as File;
			var fs:FileStream = new FileStream();
			try{
				fs.open(newFile,FileMode.WRITE);
				jsonFile = jsonFile.replace(/\n/g, File.lineEnding);
				fs.writeUTFBytes(jsonFile);
				fs.close();
				Logger.Log("File Saved: " + newFile.url, LogEntry.INFO, this);
			} catch(e:Error){
				Logger.Log(e.message, LogEntry.ERROR, this);
			}
		}

		private var saveDashboardForm:DashboardSaveUI;
		public function CloudFile_SaveDashboard():void {
			if(saveDashboardForm == null) {
				saveDashboardForm = PopUpManager.createPopUp(this, DashboardSaveUI, true) as DashboardSaveUI;
				saveDashboardForm.addEventListener(DashboardSaveUI.CLOSED, CloudFile_SaveDashboardUI_Closed);
				saveDashboardForm.addEventListener(DashboardSaveUI.COMPLETE, CloudFile_SaveDashboardUI_Complete);
				
				if(_Dashboard != null) {
					if(_Dashboard.Title != null) {
						saveDashboardForm.Title = _Dashboard.Title;
					}
					
					if(_Dashboard.ID != null) {
						saveDashboardForm.dispatchEvent(new Event(DashboardSaveUI.COMPLETE));
						saveDashboardForm.dispatchEvent(new Event(DashboardSaveUI.CLOSED));
					}
				}
			} else {
				PopUpManager.bringToFront(saveDashboardForm);
			}
		}

		public function CloudFile_SaveDashboardUI_Closed(e:Event):void {
			PopUpManager.removePopUp( saveDashboardForm );
			saveDashboardForm = null;
		}

		public function CloudFile_SaveDashboardUI_Complete(e:Event):void
		{
			var jsonFile:String = JSON.encode(_EditDocumentView.model.SaveToObject());

			if(_Dashboard == null) {
				_Dashboard = new DashboardFile();
				_Dashboard.addEventListener(CloudFile.SAVE_SUCCESS, CloudFile_SaveDashboard_SuccessEvent)
				_Dashboard.addEventListener(CloudFile.SAVE_FAILURE, CloudFile_SaveDashboard_FailureEvent)
			}

			_Dashboard.Title = saveDashboardForm.Title;
			_Dashboard.Content = jsonFile;
			_Dashboard.Attachments = _DashboardImages;

			var snapshot:ImageSnapshot = ImageSnapshot.captureImage(_EditDocumentView);
			_Dashboard.Filedata = snapshot.data;

			try{
				Logger.Log("Saving to cloud.", LogEntry.INFO, this);
				_Dashboard.Save();
			} catch(e:Error){
				Logger.Log(e.message, LogEntry.ERROR, this);
			}
		}

		public function CloudFile_SaveDashboard_SuccessEvent(event:Event):void {
			Logger.Log("Saved to cloud.", LogEntry.INFO, this);
		}

		public function CloudFile_SaveDashboard_FailureEvent(event:Event):void {
			Logger.Log("Failure saving to cloud.", LogEntry.INFO, this);
		}
		
		public function PictureClip_ImageLoaded(event:Event):void {
			var clip:PictureClip = event.target as PictureClip;
			if(clip.fileId != null) {
				return;
			}
			
			if(clip.url == null || clip.url.substring(0, 7) == "file://") {
				var clipView:PictureClipView = clip.view as PictureClipView;
				if(clipView.BitmapImage == null) {
					return;
				}
				
				var pngEncoder:PNGEncoder = new PNGEncoder();
				
				var imgFile:DashboardImage = new DashboardImage();
				imgFile.clip = clip;
				imgFile.Filedata = pngEncoder.encode( clipView.BitmapImage );
				imgFile.addEventListener(CloudFile.SAVE_SUCCESS, CloudFile_SaveAttachment_SuccessEvent);
				imgFile.addEventListener(CloudFile.SAVE_FAILURE, CloudFile_SaveAttachment_FailureEvent);
				
				imgFile.Save();
				
				_DashboardImages.push( imgFile );
			}
		}
		
		public function CloudFile_SaveAttachment(e:Event):void {
			var docView:DocumentView = _EditDocumentView as DocumentView;
			for (var clipKey:String in docView.clips) {
				if (docView.clips[clipKey]) {
					if(docView.clips[clipKey].type == "image" && (docView.clips[clipKey].url == null || docView.clips[clipKey].url.substring(0, 6) == "file://")) {
						Alert.show(docView.clips[clipKey].url);
						var clipView:PictureClipView = docView.clips[clipKey].view as PictureClipView;
						var pngEncoder:PNGEncoder = new PNGEncoder();
						
						var imgFile:DashboardImage = new DashboardImage();
						imgFile.Filedata = pngEncoder.encode( clipView.BitmapImage );

						/*imgFile.addEventListener(CloudFile.SAVE_SUCCESS, CloudFile_SaveAttachment_SuccessEvent);*/
						imgFile.addEventListener(CloudFile.SAVE_FAILURE, CloudFile_SaveAttachment_FailureEvent);
						
						imgFile.Save();
						
						docView.clips[clipKey].url = imgFile.URL;
					}
				}
			}
		}
		
		public function CloudFile_SaveAttachment_SuccessEvent(event:Event):void {;
			Logger.Log("Saved image to cloud.", LogEntry.INFO, this);
		}
		
		public function CloudFile_SaveAttachment_FailureEvent(event:Event):void {
			Logger.Log("Failure saving image to cloud.", LogEntry.INFO, this);
		}

		private var openDashboardForm:DashboardListUI;
		public function CloudFile_OpenDashboard():void {
			if(openDashboardForm == null) {
				openDashboardForm = PopUpManager.createPopUp(this, DashboardListUI, true) as DashboardListUI;
				openDashboardForm.addEventListener(DashboardSaveUI.CLOSED, CloudFile_OpenDashboardUI_Closed);
				openDashboardForm.addEventListener(DashboardSaveUI.COMPLETE, CloudFile_OpenDashboardUI_Complete);
			} else {
				PopUpManager.bringToFront(openDashboardForm);
			}
		}

		public function CloudFile_OpenDashboardUI_Closed(e:Event):void {
			PopUpManager.removePopUp( openDashboardForm );
			openDashboardForm = null;
		}

		public function CloudFile_OpenDashboardUI_Complete(e:Event):void
		{
			_Dashboard = null;
			_Dashboard = new DashboardFile();
			_Dashboard.addEventListener(CloudFile.OPEN_SUCCESS, CloudFile_OpenDashboard_SuccessEvent)
			_Dashboard.addEventListener(CloudFile.OPEN_FAILURE, CloudFile_OpenDashboard_FailureEvent)

			try{
				Logger.Log("Opening from cloud.", LogEntry.INFO, this);
				_Dashboard.Open( openDashboardForm.DashboardID );
			} catch(e:Error){
				Logger.Log(e.message, LogEntry.ERROR, this);
			}
		}

		public function CloudFile_OpenDashboard_SuccessEvent(event:Event):void {
			Logger.Log("Opened from cloud.", LogEntry.INFO, this);

			var fileData:Object = JSON.decode( _Dashboard.Content );
		    _EditDocumentView.model.LoadFromObject(fileData);
		}

		public function CloudFile_OpenDashboard_FailureEvent(event:Event):void {
			Logger.Log("Failure opening from cloud.", LogEntry.INFO, this);
		}

		public function OpenFile(file:File = null):void
		{
			if (file) {
				OpenFileObject(file);
				return;
			}
			file = File.desktopDirectory;
			file.addEventListener(Event.SELECT, OpenFileEvent);
			file.browseForOpen("Open");
		}

		protected function OpenFileEvent(event:Event):void
		{
			OpenFileObject(event.target as File);
		}

		public function OpenFileObject(file:File):void
		{
			if (!file)
				return;

			var stream:FileStream = new FileStream();
			try{
			    stream.open(file, FileMode.READ);
			    var fileData:Object = JSON.decode(stream.readUTFBytes(stream.bytesAvailable));
			    _EditDocumentView.model.LoadFromObject(fileData);
				Logger.Log("File Saved: " + file.url, LogEntry.INFO, this);
			} catch(e:Error){
				Alert.show(e.message);
			}
		}

		public function SaveImage():void
		{
			var file:File = File.desktopDirectory.resolvePath("snapshot.png");
			file.addEventListener(Event.SELECT, SaveImageEvent);
			_EditDocumentView.ClearSelection();
			file.browseForSave("Save As");
		}

		protected function SaveImageEvent(event:Event):void
		{
			var snapshot:ImageSnapshot = ImageSnapshot.captureImage(_EditDocumentView);

			var newFile:File = event.target as File;
			var fs:FileStream = new FileStream();
			try{
				fs.open(newFile,FileMode.WRITE);
				fs.writeBytes(snapshot.data, 0, snapshot.data.length);
				fs.close();
				Logger.Log("Image Saved: " + newFile.url, LogEntry.INFO, this);
			} catch(e:Error){
				trace(e.message);
			}
		}

		public function SavePDF():void
		{
			var file:File = File.desktopDirectory.resolvePath("report.pdf");
			file.addEventListener(Event.SELECT, SavePDFEvent);
			_EditDocumentView.ClearSelection();
			file.browseForSave("Save As");
		}

		protected function SavePDFEvent(event:Event):void
		{
			var snapshot:ImageSnapshot = ImageSnapshot.captureImage(_EditDocumentView, 0, new JPEGEncoder());
			var snapshotBitmap:BitmapData = ImageSnapshot.captureBitmapData(_EditDocumentView);

			var newPDF:PDF = new PDF(Orientation.LANDSCAPE, Unit.MM, Size.LETTER);
			newPDF.setDisplayMode(Display.FULL_WIDTH);

			newPDF.addPage();
//			newPDF.addImageStream(snapshot.data, ColorSpace.DEVICE_RGB, new Resize ( Mode.FIT_TO_PAGE, Position.CENTERED ));
			newPDF.addImage(new Bitmap(snapshotBitmap), new Resize ( Mode.FIT_TO_PAGE, Position.CENTERED ));
/*
			newPDF.setFont(FontFamily.ARIAL , Style.NORMAL, 12);
			newPDF.addText("Claimant Name: " + this.firstName.text + " " + lastName.text,10,40);
			newPDF.addText("Date: " + this.date.text,10,50);
			newPDF.addTextNote(48,45,100,2,"Claim Filed on: " + this.date.text + " today's date: " + new Date());
			newPDF.addText("Policy #: " + this.policyNum.text,10,60);
			newPDF.addText("Contact #: " + this.contact.text,10,70);
			newPDF.addText(this.claimNum.text,10,80);
			newPDF.addText("Claim Description:",10,90);
			newPDF.setXY(10,95);
			newPDF.addMultiCell(200,5,desc.text);
*/

			var newFile:File = event.target as File;
			var fs:FileStream = new FileStream();
			try{
				fs.open(newFile,FileMode.WRITE);
				var pdfBytes:ByteArray = newPDF.save(Method.LOCAL);
				fs.writeBytes(pdfBytes);
				fs.close();
				Logger.Log("PDF Saved: " + newFile.url, LogEntry.INFO, this);
			} catch(e:Error){
				trace(e.message);
			}
		}

		private function onDragIn(event:NativeDragEvent):void{
			NativeDragManager.acceptDragDrop(this);
		}

		private function onDrop(event:NativeDragEvent):void{
			var dropfiles:Array = event.clipboard.getData(ClipboardFormats.FILE_LIST_FORMAT) as Array;
			for each (var file:File in dropfiles){
				var ext:String = file.extension.toLowerCase();

				if (ext == "png" || ext == "jpg" || ext == "jpeg" || ext == "gif") {
					var newClip:PictureClip = _EditDocumentView.AddClipByType("image") as PictureClip;
					newClip.addEventListener(PictureClip.IMAGE_LOADED, PictureClip_ImageLoaded);
					newClip.url = file.url;
					Logger.Log("Image Dropped: " + file.url, LogEntry.INFO, this);
				} else if (ext == "clg") {
					OpenFile(file);
				} else if (ext == "csv" || ext == "tsv") {
					Logger.Log("Uploading Dropped CSV: " + file.url, LogEntry.INFO, this);
					UploadCSV(file);
				}
				else {
					//Alert.show("Unmapped Extension");
				}
			}
		}

		private function UploadDataFile():void
		{
			var file:File = File.desktopDirectory;
			file.addEventListener(Event.SELECT, UploadDataFileEvent);
			file.browseForOpen("Open");
		}

		public function UploadDataFileEvent(event:Event):void
		{
			if (!event.target || !event.target is File)
				return;

			var file:File = event.target as File;
			Logger.Log("Uploading CSV: " + file.url, LogEntry.INFO, this);
			UploadCSV(file);
		}

		private function menuShow(menuEvent:FlexNativeMenuEvent):void
		{
			if (!_EditDocumentView.IsObjectSelected()) {
//				collageMenuData..
			}
		}
		
		private function addImageClip():void {
			var img:PictureClip = _EditDocumentView.AddClipByType('image', new Rectangle(150, 150, 300, 300)) as PictureClip;
			img.addEventListener(PictureClip.IMAGE_LOADED, PictureClip_ImageLoaded);
		}

		private function menuItemClicked(menuEvent:FlexNativeMenuEvent):void
		{
			var command:String = menuEvent.item.@command;
			switch(command){
				case "cut":
					HandleCut(null);
//					NativeApplication.nativeApplication.cut();
					break;
				case "copy":
					HandleCopy(null);
//					NativeApplication.nativeApplication.copy();
					break;
				case "paste":
					HandlePaste(null);
//					NativeApplication.nativeApplication.paste();
					break;
				case "delete":
					_EditDocumentView.deleteSelected();
					break;
				case "quit":
					NativeApplication.nativeApplication.exit();
					break;
				case "new":
					_EditDocumentView.NewDocument();
					_Dashboard = null;
					_DashboardImages = null;
					_DashboardImages = new Array();
					break;
				case "about":
					break;
				case "open":
					OpenFile();
					break;
				case "save":
					SaveFile();
					break;
				case "cloudstorage_opendashboard":
					CloudFile_OpenDashboard();
					break;
				case "cloudstorage_savedashboard":
					CloudFile_SaveDashboard();
					break;
				case "saveImage":
					SaveImage();
					break;
				case "savePDF":
					SavePDF();
					break;
				case "uploadData":
					UploadDataFile();
					break;
				case "print":
					break;
				case "undo":
					break;
				case "redo":
					break;
				case "moveForward":
					_EditDocumentView.MoveSelectedForward();
					break;
				case "moveBackward":
					_EditDocumentView.MoveSelectedBackward();
					break;
				case "moveToFront":
					_EditDocumentView.MoveSelectedToFront();
					break;
				case "moveToBack":
					_EditDocumentView.MoveSelectedToBack();
					break;
				case "insertImage":
					addImageClip();
					break;
				case "insertLabel":
					_EditDocumentView.AddClipByType('label', new Rectangle(150, 150, 300, 300));
					break;
				case "insertTextBox":
					_EditDocumentView.AddClipByType('textbox', new Rectangle(150, 150, 300, 300));
					break;
				case "insertDataLabel":
					_EditDocumentView.AddClipByType('datalabel', new Rectangle(150, 150, 300, 300));
					break;
				case "insertTable":
					_EditDocumentView.AddClipByType('table', new Rectangle(150, 150, 300, 300));
					break;
				case "insertLineChart":
					_EditDocumentView.AddClipByType('linechart', new Rectangle(150, 150, 300, 300));
					break;
				case "insertScatterChart":
					_EditDocumentView.AddClipByType('scatterchart', new Rectangle(150, 150, 300, 300));
					break;
				case "insertBarChart":
					_EditDocumentView.AddClipByType('barchart', new Rectangle(150, 150, 300, 300));
					break;
				case "insertPieChart":
					_EditDocumentView.AddClipByType('piechart', new Rectangle(150, 150, 300, 300));
					break;
				case "insertGuage":
					_EditDocumentView.AddClipByType('guage', new Rectangle(150, 150, 300, 300));
					break;
				case "insertWebEmbed":
					_EditDocumentView.AddClipByType('webembed', new Rectangle(150, 150, 300, 300));
					break;
				case "insertGoogleMaps":
					_EditDocumentView.AddClipByType('googlemaps', new Rectangle(150, 150, 300, 300));
					break;
				case "fullscreen":
					Fullscreen();
					menuEvent.item.@toggled = !menuEvent.item.@toggled;
					break;
				case "refreshDatasets":
					DataEngine.LoadAllDataSets();
					break;
				case "logout":
					Session.Logout();
					break;
				case "debugger":
					var newLoggerWindow:LoggerWindow = new LoggerWindow();
					newLoggerWindow.open();
					break;
				case "hideInspector":
					inspectorCanvas.visible = !inspectorCanvas.visible;

					if (!inspectorCanvas.visible) {
						menuEvent.item.@label = "Show Inspector";
						inspectorCanvas.width = 0;
						editCanvas.setStyle("right","0");
					} else {
						menuEvent.item.@label = "Hide Inspector";
						inspectorCanvas.width = 255;
						editCanvas.setStyle("right","255");
					}

					break;
				default:
					Alert.show("Unrecognized Menu Command: " + command + "  " + menuEvent.item.@label);
			}
		}		

		public static function UploadCSV(file:File):void {
			var request:URLRequest = new URLRequest(DataEngine.getUrl("/api/v1/dataset/upload"));
			var loader:URLLoader = new URLLoader();
			var header:URLRequestHeader = new URLRequestHeader("X-Requested-With", "XMLHttpRequest");
			request.method = URLRequestMethod.POST;
            request.requestHeaders.push(header);
			
			var params:URLVariables = new URLVariables();
			params.auth_token = Session.AuthToken;
			request.data = params;

			file.addEventListener(Event.COMPLETE, FileUploadCompleteHandler);
            file.addEventListener(SecurityErrorEvent.SECURITY_ERROR, FileUploadSecurityErrorHandler);
            file.addEventListener(IOErrorEvent.IO_ERROR, FileUploadIOErrorHandler);
            file.addEventListener(HTTPStatusEvent.HTTP_STATUS, FileUploadHttpStatusHandler);
			file.upload(request,"datafile");
		}
		
        private static function FileUploadHttpStatusHandler(event:HTTPStatusEvent):void {
            event.target.removeEventListener(IOErrorEvent.IO_ERROR, FileUploadHttpStatusHandler);
			Logger.Log("Data Engine File Upload HTTP Status: " + event, LogEntry.DEBUG);
        }

		private static function FileUploadIOErrorHandler(event:IOErrorEvent):void
		{
            event.target.removeEventListener(IOErrorEvent.IO_ERROR, FileUploadIOErrorHandler);
			Logger.Log("Data Engine File Upload IO Error: " + event, LogEntry.ERROR);
		}

        private static function FileUploadSecurityErrorHandler(event:SecurityErrorEvent):void
		{
            event.target.removeEventListener(SecurityErrorEvent.SECURITY_ERROR, FileUploadSecurityErrorHandler);
			Logger.Log("Data Engine File Upload Security Error: " + event, LogEntry.ERROR);
        }

		private static function FileUploadCompleteHandler(event:Event):void
		{
			event.target.removeEventListener(Event.COMPLETE, FileUploadCompleteHandler);
            DataEngine.LoadAllDataSets();
			Logger.Log("File Upload Complete!", LogEntry.INFO);
		}
	]]>
	</mx:Script>
	<mx:FlexNativeMenu id="nativeMenu" dataProvider="{collageMenuData}" 
		labelField="@label"
		showRoot="false"
		itemClick="{menuItemClicked(event)}"
		menuShow="{menuShow(event)}"
		keyEquivalentField="@key" keyEquivalentModifiersFunction="StandardOSModifier"
		mnemonicIndexField="@mnemonicIndex"
		/>
    <mx:XML format="e4x" id="collageMenuData">
        <root>
            <menuitem label="Collage">
                <menuitem label="About" command="about" enabled="false"/>
				<menuitem type="separator"/>
				<menuitem label="Logout" command="logout" />
                <menuitem label="Quit" command="quit" key="q"/>
            </menuitem>
            <menuitem label="File">
                <menuitem label="New" command="new" key="n"/>
                <menuitem label="Open..." command="open" key="o" />
				<menuitem label="Open from Cloud..." command="cloudstorage_opendashboard" />
                <menuitem type="separator"/>
                <menuitem label="Save..." command="save" key="s" />
				<menuitem label="Save to Cloud..." command="cloudstorage_savedashboard" />
				<menuitem type="separator"/>
				<menuitem label="Export">
	                <menuitem label="PNG Image" command="saveImage"/>
	                <menuitem label="Adobe PDF" command="savePDF" />
				</menuitem>
                <menuitem type="separator"/>
                <menuitem label="Upload Dataset..." command="uploadData"/>
                <menuitem type="separator"/>
                <menuitem label="Print..." key="p" command="print" enabled="false"/>
            </menuitem>
            <menuitem label="Edit">
                <menuitem label="Undo" key="z" command="undo" enabled="false"/>
                <menuitem label="Redo" command="redo" enabled="false"/>
                <menuitem type="separator"/>
				<menuitem label="Cut" command="cut" key="x"/>
				<menuitem label="Copy" command="copy" key="c"/>
				<menuitem label="Paste" command="paste" key="v"/>
				<menuitem label="Delete" command="delete" />
                <menuitem type="separator"/>
				<menuitem label="Refresh datasets" command="refreshDatasets" key="r"/>
                <menuitem type="separator"/>
				<menuitem label="Move Selected Forward" command="moveForward"/>
				<menuitem label="Move Selected Backward" command="moveBackward"/>
				<menuitem label="Move Selected To Front" command="moveToFront"/>
				<menuitem label="Move Selected To Back" command="moveToBack"/>
            </menuitem>
            <menuitem label="Insert">
                <menuitem label="Image" command="insertImage"/>
                <menuitem label="Label" command="insertLabel"/>
                <menuitem label="Text Box" command="insertTextBox"/>
                <menuitem type="separator" />
                <menuitem label="Data Label" command="insertDataLabel"/>
                <menuitem label="Table" command="insertTable"/>
                <menuitem label="Line" command="insertLineChart"/>
                <menuitem label="Scatter Chart" command="insertScatterChart"/>
                <menuitem label="Bar Chart" command="insertBarChart"/>
                <menuitem label="Pie Chart" command="insertPieChart"/>
                <menuitem label="Guage" command="insertGuage"/>
                <menuitem type="separator" />
                <menuitem label="Web Embed" command="insertWebEmbed"/>
                <menuitem label="Google Maps" command="insertGoogleMaps"/>
            </menuitem>
            <menuitem label="View">
                <menuitem label="Fullscreen" type="check" command="fullscreen" toggled="false" key="f"/>
                <menuitem label="Hide Inspector" command="hideInspector" key="i"/>
                <menuitem type="separator" />
				<menuitem label="Debug Log Window" command="debugger" />
            </menuitem>
        </root>
    </mx:XML>
	<mx:VBox height="100%" left="0" width="32" backgroundColor="#D6D6D6" backgroundAlpha="1" horizontalScrollPolicy="off" verticalScrollPolicy="off" paddingLeft="4" paddingRight="4" paddingTop="4" paddingBottom="4">
		<mx:Image id="fullscreenBtn" source="@Embed('../../assets/themes/dark/fullscreen.png')" width="24" height="24" click="Fullscreen();" />
		<mx:HRule width="24" />
		<mx:Image id="addImageBtn" source="@Embed('../../assets/themes/dark/image.png')" width="24" height="24" click="addImageClip();" />
		<mx:Image id="addLabelBtn" source="@Embed('../../assets/themes/dark/label.png')" width="24" height="24" click="_EditDocumentView.AddClipByType('label', new Rectangle(150, 150, 300, 300));" />
		<mx:Image id="addTextBoxBtn" source="@Embed('../../assets/themes/dark/text-box.png')" width="24" height="24" click="_EditDocumentView.AddClipByType('textbox', new Rectangle(150, 150, 300, 300));" />
		<mx:Image id="addLineChartBtn" source="@Embed('../../assets/themes/dark/line-chart.png')" width="24" height="24" click="_EditDocumentView.AddClipByType('linechart', new Rectangle(150, 150, 300, 300));" />
		<mx:Image id="addScatterChartBtn" source="@Embed('../../assets/themes/dark/scatter.png')" width="24" height="24" click="_EditDocumentView.AddClipByType('scatterchart', new Rectangle(150, 150, 300, 300));" />
		<mx:Image id="addBarChartBtn" source="@Embed('../../assets/themes/dark/bar-chart.png')" width="24" height="24" click="_EditDocumentView.AddClipByType('barchart', new Rectangle(150, 150, 300, 300));" />
		<mx:Image id="addPieChartBtn" source="@Embed('../../assets/themes/dark/pie-chart.png')" width="24" height="24" click="_EditDocumentView.AddClipByType('piechart', new Rectangle(150, 150, 300, 300));" />
		<mx:Image id="addGuageBtn" source="@Embed('../../assets/themes/dark/guage.png')" width="24" height="24" click="_EditDocumentView.AddClipByType('guage', new Rectangle(150, 150, 300, 300));" />
		<mx:Image id="addTableBtn" source="@Embed('../../assets/themes/dark/datagrid.png')" width="24" height="24" click="_EditDocumentView.AddClipByType('table', new Rectangle(150, 150, 300, 300));" />
		<mx:Image id="addDataLabelBtn" source="@Embed('../../assets/themes/dark/datalabel.png')" width="24" height="24" click="_EditDocumentView.AddClipByType('datalabel', new Rectangle(150, 150, 300, 300));" />
	</mx:VBox>
	<mx:Canvas id="editCanvas" label="Canvas 1" right="265" left="32" height="100%" color="0x323232" backgroundColor="#ACACAC" backgroundAlpha="1" click="_EditDocumentView.BackgroundClick(event);">
		<mx:Box top="0" left="0" right="0" bottom="0" paddingLeft="20" paddingRight="20" paddingTop="20" paddingBottom="20"  horizontalAlign="center" verticalAlign="middle" backgroundAlpha="0" backgroundColor="0xffffff">
			<clg:EditDocumentView id="_EditDocumentView"
					width="800" height="600"
					dropShadowEnabled="true"
					click="_EditDocumentView.BackgroundClick(event);"
					backgroundColor="#FFFFFF" backgroundAlpha="1" borderThickness="0"
					horizontalScrollPolicy="off"
					verticalScrollPolicy="off" >

				<mx:HBox id="optionsBox" visible="false" paddingBottom="6" paddingTop="6">
					<!-- <mx:Image id="lockButton" source="@Embed('../../assets/themes/dark/lock.png')" click="_EditDocumentView.lockSelected()" /> -->
					<mx:Image id="deleteButton" source="@Embed('../../assets/themes/dark/delete.png')" click="_EditDocumentView.deleteSelected()" />
				</mx:HBox>
			</clg:EditDocumentView>
		</mx:Box>
	</mx:Canvas>
	<mx:Canvas id="inspectorCanvas" right="0" width="255" top="0" bottom="0" backgroundColor="#ACACAC" backgroundAlpha="1" verticalScrollPolicy="on" horizontalScrollPolicy="off">
		<mx:Box id="inspectorWindow" left="0" right="0" top="0" bottom="0" backgroundColor="#ACACAC" backgroundAlpha="1">
		</mx:Box>
	</mx:Canvas>
	
	<Collage:WelcomeScreen id="welcomeScreen" height="100%" width="100%" visible="false" />
</mx:WindowedApplication>