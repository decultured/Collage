<?xml version="1.0"?>
<mx:Application 
	xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns:Collage="Collage.*"
	xmlns:clg="Collage.Document.*"
	xmlns:degrafa="http://www.degrafa.com/2007"
	layout="absolute" 
	height="100%"
	width="100%"
	borderThickness="1"
	borderColor="#999999"
	backgroundColor="#ffffff"
	backgroundAlpha="1.0"
	applicationComplete="Initialize();"
	keyDown="HandleKeyDown(event);"
	horizontalScrollPolicy="off"
	verticalScrollPolicy="off" >
	<mx:Style source="../Common/yflexskin.css" />
	<mx:Style source="../Common/collageBasic.css" />
	<mx:Script>
	<![CDATA[
		import mx.containers.dividedBoxClasses.BoxDivider;
		import com.adobe.serialization.json.JSON;
		import Collage.Document.*;
		import Collage.Clips.Picture.*;
		import mx.controls.Alert;
		import mx.managers.PopUpManager;
		import mx.core.IFlexDisplayObject;
		import mx.events.*;
		import flash.external.*;
		import flash.events.*;
		import Collage.DataEngine.*;
		import Collage.DataEngine.Storage.*;
		import Collage.Logger.Logger;
		import Collage.Logger.LogEntry;

		private var _DashboardURL:String = "";
		private var _DashboardID:String = "";
		private var _Dashboard:DashboardFile;
		private var openDashboardForm:DashboardListUI;
		
		private var _ExtCallback_DashboardLoad;
		private var _ExtCallback_DashboardUnLoad;
		private var _ExtCallback_PopupCreate;
		private var _ExtCallback_PopupDestroy;
		
		private function PopUp_Create(parent:DisplayObject, className:Class, modal:Boolean = false):IFlexDisplayObject {
			popupOverlay.visible = true;
			
			if(_ExtCallback_PopupCreate != null) {
				ExternalInterface.call(_ExtCallback_PopupCreate);
			}
			
			var popUp:IFlexDisplayObject = PopUpManager.createPopUp(parent, className, modal);
			popUp.visible = false;
			setTimeout(function():void {
				PopUp_Center(popUp);
				popUp.visible = true;
			}, 100);
			
			return popUp;
		}
		
		private function PopUp_Center(popUp:IFlexDisplayObject):void {
			return PopUpManager.centerPopUp(popUp);
		}
		
		private function PopUp_Destroy(popUp:IFlexDisplayObject):void {
			popupOverlay.visible = false;
			
			if(_ExtCallback_PopupDestroy != null) {
				ExternalInterface.call(_ExtCallback_PopupDestroy);
			}
			
			return PopUpManager.removePopUp(popUp);
		}

		private function Initialize():void
		{
			Security.allowDomain('*');
			
			Logger.Log("Application Started", LogEntry.DEBUG   , this);
			
			_DashboardID = Application.application.parameters.id;
			
			_ExtCallback_PopupCreate = Application.application.parameters.onPopupCreate;
			_ExtCallback_PopupDestroy = Application.application.parameters.onPopupDestroy;
			_ExtCallback_DashboardLoad = Application.application.parameters.onDashboardLoad;
			_ExtCallback_DashboardUnLoad = Application.application.parameters.onDashboardUnLoad;
			
			ExternalInterface.addCallback("openDashboardList", CloudFile_OpenDashboard);

			//stage.addEventListener(KeyboardEvent.KEY_DOWN, HandleKeyDown);
			
			Session.AuthToken = Application.application.parameters.authToken;
			
			Session.events.addEventListener(Session.LOGIN_SUCCESS, HandleLoginSuccess);
			Session.events.addEventListener(Session.TOKEN_EXPIRED, HandleLoginFailure);
			Session.CheckToken();
		}
		
		public function HandleLoginSuccess(event:Event):void {
			welcomeScreen.visible = false;
			
			if(_ExtCallback_PopupDestroy != null) {
				ExternalInterface.call(_ExtCallback_PopupDestroy);
			}
			
			/*DataEngine.LoadAllDataSets();*/
			
			OpenDashboardByID(_DashboardID);
		}

		public function HandleLoginFailure(event:Event):void {
			Session.AuthToken = null;
			
			if(_ExtCallback_PopupCreate != null) {
				ExternalInterface.call(_ExtCallback_PopupCreate);
			}
			
			welcomeScreen.visible = true;
		}
		
		private function Fullscreen():void {
		}

		private function HandleKeyDown(event:KeyboardEvent):void
		{
		}
		
		public function OpenDashboardByID(dashID:String):void {
			if(dashID == null || dashID == "") {
				CloudFile_OpenDashboard();
				return;
			}
			
			if(_ExtCallback_DashboardUnLoad != null) {
				ExternalInterface.call(_ExtCallback_DashboardUnLoad);
			}
			
			_Dashboard = new DashboardFile();
			_Dashboard.addEventListener(CloudFile.OPEN_SUCCESS, OpenDashboardById_Complete);
			_Dashboard.addEventListener(CloudFile.OPEN_FAILURE, OpenDashboardById_Failure);
			_Dashboard.Open( dashID );
		}
		
		public function OpenDashboardById_Complete(event:Event):void {
			var fileData:Object = JSON.decode( _Dashboard.Content );
		    _DocumentView.model.LoadFromObject(fileData);
		
			if(_ExtCallback_DashboardLoad != null) {
				ExternalInterface.call(_ExtCallback_DashboardLoad, {'height': _DocumentView.model.height, 'width': _DocumentView.model.width});
			}
		}
		
		public function OpenDashboardById_Failure(event:Event):void {
			CloudFile_OpenDashboard();
		}
		
		public function CloudFile_OpenDashboard():void {
			if(openDashboardForm == null) {
				openDashboardForm = PopUp_Create(this, DashboardListUI, true) as DashboardListUI;
				openDashboardForm.addEventListener(DashboardSaveUI.CLOSED, CloudFile_OpenDashboardUI_Closed);
				openDashboardForm.addEventListener(DashboardSaveUI.COMPLETE, CloudFile_OpenDashboardUI_Complete);
			} else {
				PopUpManager.bringToFront(openDashboardForm);
			}
		}

		public function CloudFile_OpenDashboardUI_Closed(e:Event):void {
			PopUp_Destroy( openDashboardForm );
			openDashboardForm = null;
		}

		public function CloudFile_OpenDashboardUI_Complete(e:Event):void {
			try {
				Logger.Log("Opening from cloud.", LogEntry.INFO, this);
				OpenDashboardByID( openDashboardForm.DashboardID );
			} catch(e:Error){
				Logger.Log(e.message, LogEntry.ERROR, this);
			}
		}
	]]>
	</mx:Script>
	<mx:Canvas id="viewCanvas" label="Canvas 1" right="0" left="0" top="0" bottom="0" color="0x323232" backgroundColor="#ACACAC" backgroundAlpha="1">
		<mx:Box top="0" left="0" right="0" bottom="0" horizontalAlign="center" verticalAlign="middle" backgroundAlpha="0" backgroundColor="0xffffff">
			<clg:DocumentView id="_DocumentView" 
					width="800" height="600" 
					dropShadowEnabled="true" 
					borderStyle="solid"
					dropShadowColor="0x000000"
					backgroundColor="#FFFFFF" backgroundAlpha="1" borderThickness="0"
					horizontalScrollPolicy="off"
					verticalScrollPolicy="off" >
			</clg:DocumentView>
		</mx:Box>
	</mx:Canvas>
	<Collage:WelcomeScreen id="welcomeScreen" height="100%" width="100%" visible="false" />
	<Collage:PopUpOverlay id="popupOverlay" height="100%" width="100%" visible="false" />
</mx:Application>
