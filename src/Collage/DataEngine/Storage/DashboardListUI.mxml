<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" width="650"
	xmlns:flexlib="flexlib.containers.*"
	title="Your Dashboards"
	creationComplete="handleCreationComplete();"
	close="closeMe();">
	<mx:Script>
		<![CDATA[
			import flash.net.*;
			import flash.events.*;
			import flash.data.*;
			import com.adobe.serialization.json.JSON;
			import mx.controls.Alert;
			import mx.managers.PopUpManager;
			import mx.collections.ArrayCollection;
			import Collage.DataEngine.*;
			import Collage.Logger.*;

			public static const CLOSED:String = "CLOSED";
			public static const COMPLETE:String = "COMPLETE";

			[Bindable]
			public var dashboardList:ArrayCollection;

			public var selectedBox:Object;
			public var selectedItem:Object;

			public function get DashboardID():String { if(selectedItem != null) { return selectedItem["id"]; } else { return null; } }

			private function handleCreationComplete():void {
				// Center the TitleWindow container
				// over the control that created it.
				PopUpManager.centerPopUp(this);

				GetList();
			}

			private function closeMe():void {
				dispatchEvent(new Event(CLOSED));
			}

			private function GetList():void {
				var requestUrl:String = null;
				var request:URLRequest = new URLRequest( DataEngine.getUrl("/api/v1/storage/dashboard/list") );

				var params:URLVariables = new URLVariables();
				params.auth_token = Session.AuthToken;

				request.data = params;
	            request.requestHeaders.push(new URLRequestHeader("X-Requested-With", "XMLHttpRequest"));
				request.method = URLRequestMethod.GET;

				var loader:URLLoader = new URLLoader();

				loader.addEventListener(Event.COMPLETE, GetList_Complete);
				loader.addEventListener(SecurityErrorEvent.SECURITY_ERROR, GetList_Error);
				loader.addEventListener(IOErrorEvent.IO_ERROR, GetList_Error);

				loader.load( request );
			}

			private function GetList_Complete(event:Event):void {
				Logger.Log("DashboardListUI::GetList_Complete: " + event, LogEntry.DEBUG);

				var results:Array = JSON.decode(event.target.data);
				dashboardList = null;
				dashboardList = new ArrayCollection();

				for each(var dash:Object in results) {
					dashboardList.addItemAt( dash, 0 );
				}

				PopUpManager.centerPopUp(this);
			}

			private function GetList_Error(event:Event):void {
				Logger.Log("DashboardListUI::GetList_Error: " + event, LogEntry.DEBUG);
			}

			private function selectItem(e:Event):void {
				if(selectedBox != null) {
					selectedBox.styleName = "";
				}

				selectedItem = e.target.getRepeaterItem();

				selectedBox = e.target;
				selectedBox.styleName = "selected";
			}

			private function openDashboard(e:Event):void {
				if(selectedItem == null) {
					return;
				}

				dispatchEvent(new Event(COMPLETE));
				closeMe();
			}

		]]>
	</mx:Script>
	<mx:Style>
		.selected {
			backgroundColor: #0DA0D8;
			fontColor: #FFFFFF;
		}
	</mx:Style>
		<mx:Canvas height="300" width="100%" horizontalScrollPolicy="off">
			<flexlib:FlowBox
				horizontalGap="3" verticalGap="3"
				width="100%"
				height="100%"
				horizontalScrollPolicy="off"
				paddingTop="5" paddingBottom="5" paddingLeft="5" paddingRight="5">
				<mx:Repeater id="dashListRep" dataProvider="{dashboardList}">
					<mx:VBox click="{selectItem(event)}" doubleClick="{selectItem(event); openDashboard(event);}" horizontalScrollPolicy="off" verticalScrollPolicy="off">
						<mx:Image  width="200" height="150" source="{DataEngine.getUrl('api/v1/storage/dashboard/'+ String(dashListRep.currentItem.id) +'/snapshot?auth_token='+ Session.AuthToken)}"/>
						<mx:Label text="{String(dashListRep.currentItem.title).substr(0, 15)}" width="100%" fontSize="12" textAlign="center" />
					</mx:VBox>
				</mx:Repeater>
			</flexlib:FlowBox>
		</mx:Canvas>
		<mx:Spacer height="10" width="100%" />
		<mx:HBox horizontalAlign="right" width="100%">
			<mx:Button id="cancelBtn" click="{closeMe();}" label="Cancel" fontSize="16" />
			<mx:Button id="openDashboardBtn" click="{openDashboard(event);}" label="Open" fontSize="16" />
		</mx:HBox>
</mx:TitleWindow>
