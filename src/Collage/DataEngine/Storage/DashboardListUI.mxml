<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" width="650"
	xmlns:flexlib="flexlib.containers.*"
	title="Your Dashboards"
	creationComplete="handleCreationComplete();"
	close="closeMe();">
	<mx:Script>
		<![CDATA[
			import flash.net.*;
			import flash.events.*;
			import mx.core.IFlexDisplayObject;
			import com.adobe.serialization.json.JSON;
			import mx.controls.Alert;
			import mx.managers.PopUpManager;
			import mx.collections.ArrayCollection;
			import Collage.DataEngine.*;
			import Collage.Logger.*;

			public static const CLOSED:String = "CLOSED";
			public static const COMPLETE:String = "COMPLETE";

			[Bindable]
			public var dashboardList:ArrayCollection;

			public var selectedBox:Object;
			[Bindable]
			public var selectedItem:Object;

			public function get DashboardID():String { if(selectedItem != null) { return selectedItem["id"]; } else { return null; } }

			private function handleCreationComplete():void {
				dashboardList = null;
				dashboardList = new ArrayCollection();
				
				GetList();
			}

			private function closeMe():void {
				dispatchEvent(new Event(CLOSED));
			}

			private function GetList():void {
				var requestUrl:String = null;
				var request:URLRequest = new URLRequest( DataEngine.getUrl("/api/v1/storage/dashboard/list") );

				var params:URLVariables = new URLVariables();
				params.aT = Session.AuthToken;

				request.data = params;
	            request.requestHeaders.push(new URLRequestHeader("X-Requested-With", "XMLHttpRequest"));
				request.method = URLRequestMethod.GET;

				var loader:URLLoader = new URLLoader();

				loader.addEventListener(Event.COMPLETE, GetList_Complete);
				loader.addEventListener(SecurityErrorEvent.SECURITY_ERROR, GetList_Error);
				loader.addEventListener(IOErrorEvent.IO_ERROR, GetList_Error);

				loader.load( request );
			}

			private function GetList_Complete(event:Event):void {
				Logger.Log("DashboardListUI::GetList_Complete: " + event, LogEntry.DEBUG);

				var results:Array = JSON.decode(event.target.data);

				for each(var dash:Object in results) {
					dashboardList.addItemAt( dash, 0 );
				}
				
				dashListRep.dataProvider = dashboardList;
			}

			private function GetList_Error(event:Event):void {
				Logger.Log("DashboardListUI::GetList_Error: " + event, LogEntry.DEBUG);
			}

			private function selectItem(event:Event):void {
				try {
					if(selectedBox != null) {
						selectedBox.styleName = "";
					}
					
					selectedItem = event.currentTarget.getRepeaterItem();
					
					var index:int = 0;
					for each(var item:Object in dashboardList) {
						if(item.id == selectedItem.id) {
							break;
						}
						index++;
					}
					
					selectedBox = dashboardItemBox[index];
					selectedBox.styleName = "selected";
				} catch(error:Error) {
					Alert.show("error = "+ error);
				}
			}

			private function openDashboard(e:Event):void {
				if(selectedItem == null) {
					return;
				}

				dispatchEvent(new Event(COMPLETE));
				closeMe();
			}

		]]>
	</mx:Script>
	<mx:Style>
		.selected {
			backgroundColor: #0DA0D8;
			fontColor: #FFFFFF;
		}
	</mx:Style>
		<mx:Canvas height="300" width="100%" horizontalScrollPolicy="off">
			<mx:Tile direction="horizontal" horizontalGap="10" verticalGap="15" 
				color="0x323232" paddingTop="10" paddingBottom="10" paddingLeft="10" paddingRight="10">
				<mx:Repeater id="dashListRep" startingIndex="0">
					<mx:VBox id="dashboardItemBox" click="{selectItem(event)}" horizontalScrollPolicy="off" verticalScrollPolicy="off" paddingLeft="3" paddingRight="3" paddingBottom="3" paddingTop="3">
						<mx:Image click="{selectItem(event)}" width="200" height="150" source="{DataEngine.getUrl('api/v1/storage/dashboard/'+ String(dashListRep.currentItem.id) +'/snapshot.png?aT='+ Session.AuthToken)}"/>
						<mx:Label click="{selectItem(event)}" text="{String(dashListRep.currentItem.title).substr(0, 15)}" width="100%" fontSize="12" textAlign="center" />
					</mx:VBox>
				</mx:Repeater>
			</mx:Tile>
		</mx:Canvas>
		<mx:Spacer height="10" width="100%" />
		<mx:HBox horizontalAlign="right" width="100%">
			<mx:Button id="cancelBtn" click="{closeMe();}" label="Cancel" fontSize="16" />
			<mx:Button id="openDashboardBtn" click="{openDashboard(event);}" label="Open" fontSize="16" enabled="{selectedItem != null}" />
		</mx:HBox>
</mx:TitleWindow>
