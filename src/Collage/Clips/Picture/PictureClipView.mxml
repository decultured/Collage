<?xml version="1.0" encoding="utf-8"?>
<clg:ClipView xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns:clg="Collage.Clip.*"
	horizontalScrollPolicy="off"
	verticalScrollPolicy="off"
	creationComplete="Initialize()" >
	
	<mx:Script>
		<![CDATA[
		import mx.controls.Alert;
		import mx.core.*;
		import mx.events.PropertyChangeEvent;
		
		public var _BitmapData:BitmapData = null;
		
		public function get BitmapImage():BitmapData {
			return _BitmapData;
		}
		
		public var _Loaded:Boolean = false;
		
		public function Initialize():void
		{
			LoadImage();
		}
		
		private function LoadImage():void
		{
			if (!displayImage)
				return;
			
			var pictureModel:PictureClip = _Model as PictureClip;
			pictureModel.imageLoaded = false;
			if (pictureModel.url) {
				displayImage.load(pictureModel.url);
				displayImage.addEventListener(Event.COMPLETE, Complete);
			} else if(_BitmapData) {
				var bitmap:BitmapAsset = new BitmapAsset(_BitmapData);
				pictureModel.imageLoaded = true;
				pictureModel.aspectRatio = _BitmapData.width / _BitmapData.height;
                if (bitmap != null && bitmap.smoothing == false) {
                    bitmap.smoothing = true;
                }
				displayImage.source = bitmap;
				_Model.dispatchEvent(new Event(PictureClip.IMAGE_LOADED));
				super.onModelChange(null);
			} 
		}

		protected override function onModelChange( event:PropertyChangeEvent):void
		{
			if(event && event.property == "url") {
				LoadImage();
			}
			super.onModelChange(event);
		}

		private function Complete(event:Event):void
		{
			_Loaded = true;

			var pictureModel:PictureClip = _Model as PictureClip;
			displayImage.removeEventListener(Event.COMPLETE, Complete);

			if (displayImage.content is Bitmap) {
				pictureModel.imageLoaded = true;
                var bitmap:Bitmap = displayImage.content as Bitmap;
                if (bitmap != null && bitmap.smoothing == false) {
                    bitmap.smoothing = true;
                }
				if (bitmap.height && bitmap.width) {
					pictureModel.aspectRatio = bitmap.width / bitmap.height;
				}
				_BitmapData = bitmap.bitmapData;
				_Model.dispatchEvent(new Event(PictureClip.IMAGE_LOADED));
            }
			super.onModelChange(null);
		}

		public override function LoadFromData(data:Object):Boolean
		{
			if (data is BitmapData)
			{
				_Loaded = false;
				_BitmapData = data as BitmapData;
				LoadImage();
				return true;
			}
			return false;
		}
		]]>
    </mx:Script>
	<mx:Image id="displayImage" horizontalAlign="left" verticalAlign="top" top="0" left="0" right="0" bottom="0" />
	<mx:Canvas visible="{!model.imageLoaded}" top="0" left="0" right="0" bottom="0" backgroundColor="0x000000" backgroundAlpha="0.2" />
</clg:ClipView>